apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    flavorDimensions "default"

    defaultConfig {
        applicationId "com.insperity.escmobile"
        minSdkVersion 20
        targetSdkVersion 29
        multiDexEnabled true
        versionCode 6
        versionName "1.2.1"
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile rootProject.file("app/release.keystore")
            storePassword System.getProperty("APPCENTER_KEYSTORE_PASSWORD")
            keyAlias System.getProperty("APPCENTER_KEY_ALIAS")
            keyPassword System.getProperty("APPCENTER_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            buildConfigField "Integer", "BUILD_ID", "$System.env.BUILD_ID"
            buildConfigField "Integer", "DB_VERSION", "2"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            buildConfigField "String", "LEGAL_ENDPOINT", "\"http://www.insperity.com/service-technology/mobile/legalnotice.html\""
            buildConfigField "String", "FULL_ESC_WEBSITE_URL", "\"https://portal.insperity.com?fromMobile=true\""
            signingConfig signingConfigs.release
            buildConfigField "String", "ENDPOINT", "\"https://timestar.insperity.com/Hermes/mobile/v2/\""
            resValue "string", "flavor_app_name", "OnTrak"
        }
        rc.initWith(buildTypes.release)
        rc {
            applicationIdSuffix ".rc"
            versionNameSuffix " RC"
            debuggable true
            signingConfig signingConfigs.debug
            resValue "string", "flavor_app_name", "OnTrak RC"
        }
        debug.initWith(buildTypes.rc)
        debug {
            applicationIdSuffix ""
            versionNameSuffix " DEV"
            buildConfigField "String", "ENDPOINT", "\"http://wdvw3itat01.corporate.administaff.com/Hermes/mobile/v2/\""
            resValue "string", "flavor_app_name", "OnTrak DEV"
            FirebasePerformance {
                instrumentationEnabled false
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //noinspection GradleCompatible
    implementation 'androidx.appcompat:appcompat:1.3.1'
    //noinspection GradleCompatible
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    //noinspection GradleCompatible
    implementation 'com.google.android.material:material:1.4.0'
    //noinspection GradleCompatible
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    //noinspection GradleCompatible
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.google.android.gms:play-services-analytics:18.0.1'
    implementation 'com.google.android.gms:play-services-gcm:17.0.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation('com.google.code.gson:gson:2.8.6') {
        force = true
    }
    implementation 'com.squareup:android-times-square:1.6.5@aar'
    implementation 'com.code-troopers.betterpickers:library:3.1.0'
    implementation 'net.danlew:android.joda:2.9.3'
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'io.reactivex:rxjava:1.1.8'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.jakewharton.timber:timber:4.6.1'
    implementation 'com.avast:android-styled-dialogs:2.3.3'
    implementation 'com.jakewharton:butterknife:10.2.1'
    implementation 'com.github.JakeWharton:ViewPagerIndicator:2.4.1'
    implementation 'com.google.dagger:dagger:2.39.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.google.firebase:firebase-core:20.1.2'
    implementation 'com.google.firebase:firebase-perf:20.0.6'
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    implementation("com.crashlytics.sdk.android:crashlytics:$crashlytics_version"){
        transitive = true
    }
    implementation('com.google.firebase:firebase-analytics:20.1.2') {
        transitive = true
    }
    implementation('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0@aar') {
        transitive = true
    }
    kapt 'com.jakewharton:butterknife-compiler:10.2.1'
    kapt 'com.google.dagger:dagger-compiler:2.14.1'
    compileOnly 'javax.annotation:jsr250-api:1.0'

    implementation('com.tomerrosenfeld.customanalogclockview:custom-analog-clock-view:1.04') {
        exclude group: 'com.android.support'
    }

    testImplementation 'junit:junit:4.13'

    // here until project dependency versions are up to date
    compileOnly 'com.github.pengrad:jdk9-deps:1.0'
}